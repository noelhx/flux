namespace flux
	include 'flux.lm'
end

include 'influxql.lm'

parse P: program [stdin]

if ( !P ) {
	print "[error]
	exit( 1 )
}

void normalize_conjunctions( Expr: ref<expr> )
{
	Modified: bool = true
	while ( Modified ) {
		Modified = false
		for SLE: conjunction_expression in Expr {
			if match SLE
				[ `( CE: conjunction_expression `) ] 
			{
				SLE = CE
				Modified = true
			}
			else if match SLE
				[ LeftCE: conjunction_expression `AND `( InnerCE: conjunction_expression `) ] 
			{
				# Place LeftCE at the end of the InnerCE list.
				for Tail: conjunction_expression in InnerCE {
					if match Tail [Last: comparison_expression] {
						Tail = cons conjunction_expression " [LeftCE] AND [Last] "
						# Must break here to end the replacement loop.
						break
					}
				}

				SLE = InnerCE
				Modified = true
			}
		}
	}
}

bool time_constraint( Any: any )
{
	for UE: unary_expr in Any {
		if match UE "time"
			return true
	}
	return false
}

void disjunctions_of_time( Expr: expr )
{
	Found: bool = false
	for DE: disjunction_expression in Expr {
		if match DE
			[Left: disjunction_expression `OR Right: conjunction_expression]
		{
			if time_constraint( Left ) || time_constraint( Right ) {
				print "ERROR: disjunction of time found: [^DE]
				Found = true
			}
		}
	}
	if ( Found )
		exit( 1 )
}

# 1. Normalize conjunctions.
# 2. Extract time constraints. Fail if we find any inside a disjunction.

# 1. Pull and apply the time constraint. Replace with true. Expect conjunction
# 2. Remaining where clause items.
# 3. Apply Group By.
# 4. Select Expressions
#
for SS: select_stmt in P {
	print "select statement:\n[^SS]\n"

	WC: where_clause = SS.opt_where_clause.where_clause
	if WC {
		Expr: expr = WC.expr

		normalize_conjunctions( Expr )
		print "normalized where clause expression:\n[^Expr]\n"
		disjunctions_of_time( Expr )
	}
}

